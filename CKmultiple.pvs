CKmultiple: THEORY
BEGIN
  IMPORTING AssetMapping
  
  AssetIfdef: TYPE = [AssetName,boolean]

  AssetNames: TYPE = finite_sets[AssetName].finite_set

  ans: VAR AssetNames
  n,an: VAR AssetName
  a,b,a1,a2: VAR Asset
  tag: VAR bool
  am,amt,am2,amt2: VAR AM
  t: VAR AssetIfdef
  assets: VAR finite_sets[Asset].finite_set 

  selectAssets(ans,am,amt):AM = 
    overw(filter(ans,am),amt)


  preprocessfile(a,tag):Asset

  preprocessRefinement(a,b):bool =
   FORALL(tag):|-(preprocessfile(a,tag),preprocessfile(b,tag))
 
  ifdefrefinement: AXIOM
    |-(a,b) <=> FORALL(tag):|-(preprocessfile(a,tag),preprocessfile(b,tag))

  preprocess(t,am,amt):AM = 
    IF (dom(amt)(t`1)) THEN ow((t`1,preprocessfile(map(amt,singleton(t`1)),t`2)),amt)
    ELSE amt
    ENDIF

  keepdom: LEMMA
    dom(amt) = dom(preprocess(t,am,amt))
  
  Transformations: DATATYPE
   BEGIN
    IFDEF(an:AssetName,tag:boolean): IFDEF?
    SELECT(ans:AssetNames): SELECT?
   END Transformations

  ckevaluation(ts:Transformations,am,amt:AM):AM =
    CASES ts OF 
      IFDEF(an,tag): preprocess((an,tag),am,amt),
      SELECT(ans): selectAssets(ans,am,amt)
    ENDCASES

  cknames(ts:Transformations):set[AssetName] =
     CASES ts OF 
      IFDEF(an,tag): singleton(an),
      SELECT(ans): ans
    ENDCASES

  ckPreserves: THEOREM
   FORALL (am1: AM, am2: AM, t: Transformations, amt1: AM, amt2: AM):
    (am1 |> am2) AND (amt1 |> amt2) =>
     (ckevaluation(t, am1, amt1) |> ckevaluation(t, am2, amt2));


  IMPORTING CKtrans{{
  	    Transformation:=Transformations,
	    transform:=ckevaluation,
            names:=cknames
  }}
  
  fm: VAR WFM
  ck,ck2: VAR CK
  it,it2: VAR Item
  c: VAR Configuration
  anSet: VAR finite_sets[AssetName].finite_set
  s: VAR set[Configuration]

  domainPreserved: LEMMA FORALL ck,am,c,amt: subset?(dom(semanticCK(ck,am,c,amt)),union(dom(am),dom(amt)))  

  domSubset: LEMMA FORALL ck,am,c,amt: subset?(dom(amt),dom(semanticCK(ck,am,c,amt)))

  
  unusedAssets: LEMMA FORALL ck,am,c,amt,anSet: subset?(domain(semanticCK(ck,am,c,amt)),anSet)
                                                =>
                                                (semanticCK(ck,am,c,amt) = semanticCK(ck,filter(anSet,am),c,amt))


  sameAmtSameDom: LEMMA FORALL ck,am,c,amt,amt2: domain(amt) = domain(amt2) 
                                                  => 
                                                  domain(semanticCK(ck,am,c,amt)) = domain(semanticCK(ck,am,c,amt2))  



  amNotChangesNames: LEMMA FORALL fm,ck,am,am2,amt: 
                              FORALL c: (semantics(fm)(c) AND 
                                        wfCK(fm,am,ck) AND
                                        wfCK(fm,am2,ck)) 
                                        =>
                                        (domain(semanticCK(ck,am,c,amt)) = domain(semanticCK(ck,am2,c,amt)))


  falseExpsCK: LEMMA FORALL(am,ck,ck2,s):
    (
       FORALL c: s(c) => 
                         ( 
                            (FORALL it : diffIts(it) => NOT satisfies(exp(it),c))
                            => 
                            (FORALL amt: semanticCK(ck,am,c,amt) = semanticCK(ck2,am,c,amt))
                         )
      
    ) WHERE diffIts = symmetric_difference(list2set(ck),list2set(ck2))

END CKmultiple
