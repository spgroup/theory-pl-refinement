SPLPartialRefinementWeak[Conf: TYPE,
                     FM: TYPE,
                     {||}: [FM -> set[Conf]],
                     Asset: TYPE,
                     AssetName: TYPE,
                     CK: TYPE,
                     (IMPORTING maps[AssetName,Asset]) [||]: [CK -> [mapping -> [Conf -> finite_sets[Asset].finite_set]]] ]: THEORY
BEGIN

% Partial SPL Refinement
    IMPORTING maps

    AM:TYPE = maps[AssetName,Asset].mapping
    CM:TYPE = maps[Conf,Conf].mapping
     
   % --------------------------------------------------------------------------------------------------------
   % ------------------------------------------------VARIABLES-----------------------------------------------
   % -------------------------------------------------------------------------------------------------------- 
    c: VAR Conf
    m,n: VAR CM
    fm,fm1,fm2: VAR FM
    am,am1,am2: VAR AM

    IMPORTING maps_identity[Conf]
    IMPORTING maps_composite[Conf,Conf,Conf]
    IMPORTING SPLRefinementFiniteFM[Conf,FM, {||}, Asset, AssetName, CK,[||]]
    
    pl, pl1, pl2, pl3: VAR PL

   % --------------------------------------------------------------------------------------------------------
   % ------------------------------------------------RELATION DEF--------------------------------------------
   % --------------------------------------------------------------------------------------------------------

   % Partial Refinement Relation - this definition considers a mapping that maps configurations from the two PLs.
    weakPartialRefinement(pl1,pl2:PL,m:{pairs:CM | subset?(domain(pairs),{||}(F(pl1))) AND subset?(image(pairs),{||}(F(pl2)))}) : bool = 
    	FORALL c : domain(m)(c)  => 
            (
               prod(pl1,c) 
               |- 
               prod(pl2,getRight(m,c))
            )  

   % --------------------------------------------------------------------------------------------------------
   % ------------------------------------------------PROPERTIES----------------------------------------------
   % --------------------------------------------------------------------------------------------------------
   
   %--------------------------------------------------PREORDER-----------------------------------------------

   % Theorem <weak partial refinement is reflexive>
    weakPartRefReflexive: THEOREM FORALL pl,(m:CM | subset?(domain(m), {||}(F(pl))) AND subset?(image(m), {||}(F(pl)))) : 
                                         identity?(m) => weakPartialRefinement(pl,pl,m)
   % Theorem <weak partial refinement is transitive>
    weakPartRefTransitive: THEOREM FORALL pl1, pl2, pl3, 
                                            (m:CM | subset?(domain(m), {||}(F(pl1))) AND subset?(image(m), {||}(F(pl2)))),
                                            (n:CM | subset?(domain(n), {||}(F(pl2))) AND subset?(image(n), {||}(F(pl3)))) :
                                     ( 
                                          (
                                            weakPartialRefinement(pl1,pl2,m) 
                                           AND 
                                            weakPartialRefinement(pl2,pl3,n) 
                                           AND 
                                            image(m) = domain(n)
                                          )
                                          => weakPartialRefinement(pl1,pl3,q)
                                      ) WHERE q = composeMaps(m,n)
   

END SPLPartialRefinementWeak
